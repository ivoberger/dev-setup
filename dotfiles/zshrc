# --- Early PATH setup (needed before install checks) ---
export PATH="$HOME/.local/bin:$HOME/.fzf/bin:$PATH"

# --- Auto-install missing dependencies ---
function _ensure_installed() {
  local name="$1" check_cmd="$2" install_cmd="$3"
  if ! command -v "$check_cmd" &>/dev/null; then
    echo "[$name] not found."
    echo -n "Install $name? [Y/n] "
    read -r answer
    if [[ -z "$answer" || "$answer" =~ ^[Yy]$ ]]; then
      eval "$install_cmd"
    else
      echo "Skipping $name. Some features may not work."
    fi
  fi
}

_ensure_installed "starship" "starship" \
  'curl -sS https://starship.rs/install.sh | sh -s -- -y'

# Add fzf to PATH if installed via git clone

_ensure_installed "fzf" "fzf" \
  'git clone --depth 1 https://github.com/junegunn/fzf.git ~/.fzf && ~/.fzf/install --all --no-bash --no-fish'

_ensure_installed "direnv" "direnv" \
  'curl -sfL https://direnv.net/install.sh | bash'

_ensure_installed "zoxide" "zoxide" \
  'curl -sSf https://raw.githubusercontent.com/ajeetdsouza/zoxide/main/install.sh | sh'

unfunction _ensure_installed

# --- Auto-update check (every 28 days) ---
_UPDATE_STAMP="${XDG_CACHE_HOME:-$HOME/.cache}/zsh_update_stamp"
_UPDATE_INTERVAL=$((28 * 24 * 60 * 60))

function _check_updates() {
  local now=$(date +%s)
  local last_update=0

  if [[ -f "$_UPDATE_STAMP" ]]; then
    last_update=$(cat "$_UPDATE_STAMP")
  fi

  if (( now - last_update > _UPDATE_INTERVAL )); then
    echo "It's been a while since your shell tools were updated."
    echo -n "Run updates now? [Y/n] "
    read -r answer
    if [[ -z "$answer" || "$answer" =~ ^[Yy]$ ]]; then
      echo "Updating antidote plugins..."
      antidote update

      if command -v starship &>/dev/null; then
        echo "Updating starship..."
        curl -sS https://starship.rs/install.sh | sh -s -- -y
      fi

      if [[ -d ~/.fzf ]]; then
        echo "Updating fzf..."
        (cd ~/.fzf && git pull && ./install --all --no-bash --no-fish)
      fi

      if command -v direnv &>/dev/null; then
        echo "Updating direnv..."
        curl -sfL https://direnv.net/install.sh | bash
      fi

      if command -v zoxide &>/dev/null; then
        echo "Updating zoxide..."
        curl -sSf https://raw.githubusercontent.com/ajeetdsouza/zoxide/main/install.sh | sh
      fi

      echo "$now" > "$_UPDATE_STAMP"
      echo "All updates complete."
    else
      # Snooze for another 28 days
      echo "$now" > "$_UPDATE_STAMP"
    fi
  fi
}

_check_updates
unfunction _check_updates

# --- Source local config (before compinit so local fpath entries are picked up) ---
[[ -f ~/.env.zsh ]] && source ~/.env.zsh
[[ -f ~/.local.env.zsh ]] && source ~/.local.env.zsh

# --- Completion (must be before antidote load) ---
autoload -Uz compinit && compinit

# --- Antidote plugin manager ---
# Bootstrap antidote if not installed
ANTIDOTE_HOME="${ZDOTDIR:-$HOME}/.antidote"
if [[ ! -d "$ANTIDOTE_HOME" ]]; then
  git clone --depth=1 https://github.com/mattmc3/antidote.git "$ANTIDOTE_HOME"
fi
source "$ANTIDOTE_HOME"/antidote.zsh

# Load plugins from ~/.zsh_plugins.txt
antidote load

# --- Prompt ---
eval "$(starship init zsh)"

# --- fzf ---
if command -v fzf &>/dev/null; then
  if [[ $(fzf --version | cut -d. -f1-2 | tr -d .) -ge 048 ]]; then
    eval "$(fzf --zsh)"
  else
    # Older fzf: source scripts directly
    [[ -f ~/.fzf.zsh ]] && source ~/.fzf.zsh
  fi
fi

# --- direnv ---
eval "$(direnv hook zsh)" 2>/dev/null

# --- zoxide ---
eval "$(zoxide init zsh)" 2>/dev/null

# --- PATH ---
path=(~/bin ~/.yarn/bin $path)

# --- Environment ---
export GPG_TTY=$TTY
export NVM_COMPLETION=true
export NVM_AUTO_USE=true
export SSH_AUTH_SOCK="$XDG_RUNTIME_DIR/ssh-agent.socket"
unset BROWSER

# --- Key bindings ---
# Backward kill word
bindkey '^H' backward-kill-word        # Ctrl+Backspace / Ctrl+H
bindkey '^[^?' backward-kill-word      # Ctrl+Alt+Backspace

# Undo/redo
bindkey '^/' undo                      # Ctrl+/
bindkey '^[/' redo                     # Alt+/

# --- Autoload ---
autoload -Uz zmv

# --- Functions ---
function md() { [[ $# == 1 ]] && mkdir -p -- "$1" && cd -- "$1" }
compdef _directories md

function git_clean() {
  git fetch $1 -p && for branch in $(git for-each-ref --format '%(refname) %(upstream:track)' refs/heads | awk '$2 == "[gone]" {sub("refs/heads/", "", $1); print $1}'); do git branch -D $branch; done
}

function adb_forward() {
  socat -d -d TCP-LISTEN:5037,reuseaddr,fork TCP:$(cat /etc/resolv.conf | tail -n1 | cut -d " " -f 2):5037
}

# --- Aliases ---
alias tree='tree -a -I .git'
alias gco='git_clean origin'
alias gc='git commit'
alias gcm='git commit -a --message'
alias gca='git commit --amend --no-edit'
alias gcae='git commit --amend'
alias gcaa='git commit --amend --no-edit --all'
alias gpr='git pull --rebase'
alias gpf='git push --force-with-lease'
alias gcaapf='git commit --amend --no-edit --all && git push --force-with-lease'
alias gr='git rebase'
alias grd='git rebase development'
alias gcd='git checkout development'
alias grh1='git reset --soft HEAD~1'
alias pamper='gco && gpr && yarn'
alias ls="${aliases[ls]:-ls} -A"

# --- Shell options ---
setopt glob_dots     # no special treatment for file names with a leading dot
setopt no_auto_menu  # require an extra TAB press to open the completion menu

# --- Bun ---
[ -s "$HOME/.bun/_bun" ] && source "$HOME/.bun/_bun"
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"
